# Maintainer: Edward Pacman <micro(dot)fedora(at)gmail(dot)com>

_pkgbase=netfilter-full-cone-nat
pkgname=netfilter-full-cone-nat-dkms
pkgver=r70.d4daedd
pkgrel=1
pkgdesc="A kernel module that turns MASQUERADE into full cone SNAT"
arch=('x86_64')
url="https://github.com/Chion82/netfilter-full-cone-nat"
license=('GPL2')
depends=('dkms')
conflicts=("${_pkgbase}")
source=('dkms.conf'
        'Makefile.patch'
        'git+https://github.com/Chion82/netfilter-full-cone-nat')
md5sums=('426b17a1544478bcbe5ecc56fd392db0'
         'ca672d85174ea52d57758bd53c0a0ed9'
         'SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd ${srcdir}/${_pkgbase}
  patch -p1 -i "${srcdir}"/Makefile.patch
}

package() {
  # Copy dkms.conf
  install -Dm644 dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  # Set name and version
  sed -e "s/@_PKGBASE@/${_pkgbase}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  # Copy sources (including Makefile)
  cp -r ${_pkgbase}/* "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
}
